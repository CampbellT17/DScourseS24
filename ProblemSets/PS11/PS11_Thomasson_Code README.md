*Econ Project Code;

options nocenter ls=max; *Richard says this formatting is good practice;

*First, we run the replication as instructed in Hanlon (2005) (I go through 2022 just to see how data holds 20+ years later))

*get Compustat data;

data income;
    set comp.funda;
    keep gvkey prcc_f cusip tic sich datadate fic fyear ibc fyr at ni pi ibc ajex tlcf csho txc txdfed txdfo ceq txdi  txp txt oiadp oancf txpd xidoc;
    where indfmt="INDL" and datafmt='STD' and consol='C' and curcd ="USD" and popsrc='D' and fyear between 1993 and 2001;
    if not missing(ni+at+pi);
run;

proc sql;
    create table income as
        select a.*, b.sic
        from income a
        left join comp.namesd as b
        on a.gvkey = b.gvkey;
quit;

PROC SORT; BY gvkey fyear;

*Check for duplicates;

proc sort data=income
          out=income
          nodupkey; 
          by gvkey fyear; 
run; 

*Load repeatcheck;

%include "/home/ou/campthom17/macros/_repeatcheck.sas";

*Run repeatcheck Macro;
 
%repeatcheck(income, gvkey fyear); 

*Create Needed Financial Variables;

data income; 
	set income;
	mve=prcc_f*csho;
	if not missing(txdfed+txdfo) then dtax=txdfed+txdfo;
	else dtax = txdi;
	ctax=txt-dtax;
	if fyear<2018 then dte=dtax/0.35; 
	else dte=dtax/0.21;
	if fyear<2018 then ti=ctax/0.35; 
	else ti=ctax/0.21;
	ptcf=oancf+txpd-xidoc;
	ptacc=pi-ptcf;
	if sich =. then sich = sic;
run;

*Create Lag Variables;

proc sql;
    create table income as
        select a.*, b.at as atm1
        from income as a left join income as b
        on a.cusip=b.cusip and a.fyear=b.fyear + 1 and a.fyr=b.fyr
        order by cusip, fyear;
quit;

*Create Lead Variables;

proc sql;
    create table income as
        select a.*, b.pi as pip1
        from income as a left join income as b
        on a.cusip=b.cusip and a.fyear=b.fyear + 1 and a.fyr=b.fyr
        order by cusip, fyear;
quit;

*Set filters described by Hanlon (2005);

data income;
	set income;
	if fyear = 1993 or fyear = 2001 then delete;
	sich2 = int(sich/100);
	if sich2 =. then delete;
	if sich2 = (49+60+61+62+63+64) then delete;
	if sich = 9631 then delete; 
	if mve =. then delete;
	if pi le 0 then delete;
	if ctax le 0 then delete;
	if tlcf < 0 then delete;
	if tlcf > 0 then delete;
	if at le 0 then delete;
	if atm1 le 0 then delete;
	if missing(pip1+atm1) then delete;
run;

*Calculate Accrual and Cash Flow Components (Using Instructions from Hanlon (2005));

data income;
	set income;
	aveta = (at+atm1)/2;
	scaled_pip1 = pip1/aveta;
	scaled_pi = pi/aveta;
	scaled_ptcf = ptcf/aveta;
	scaled_ptacc = ptacc/aveta;
	scaled_dte = dte/aveta;
	scaled_ti = ti/aveta;
run;

*Load Winsor;
%include "/home/ou/campthom17/macros/_winsor.sas";

*Run Winsor Macro;

options mprint; *Will report in log file the code generated by the macro.;
%winsor(dsetin=income, dsetout=incomew, byvar=none, vars=scaled_pip1 scaled_pi scaled_ptcf scaled_ptacc scaled_dte, type=winsor, pctl=1 99);

*Calculate Book-Tax Differences and Sort into Quintiles;

proc rank data=incomew
   out=incomew_ranked
   groups=5;
   var scaled_dte;
   ranks btd_quintile;
run;

*Create Dummy Variables and Related Interaction Terms;

data incomew_ranked;
	set incomew_ranked;
	if btd_quintile = 4 then lpbtd = 1;
	else lpbtd = 0;
	if btd_quintile = 0 then lnbtd = 1;
	else lnbtd = 0;
	if btd_quintile = 1 or btd_quintile = 2 or btd_quintile = 3 then small_btd = 1;
	else small_btd = 0;
	if btd_quintile = 0 or btd_quintile = 4 then lbtd = 1;
	else lbtd = 0;
	pi_lnbtd = scaled_pi * lnbtd;
	pi_lpbtd = scaled_pi * lpbtd;
	pi_lbtd = scaled_pi * lbtd;
	ptcf_lnbtd = scaled_ptcf * lnbtd;
	ptcf_lpbtd = scaled_ptcf * lpbtd;
	ptcf_lbtd = scaled_ptcf * lbtd;
	ptacc_lnbtd = scaled_ptacc * lnbtd;
	ptacc_lpbtd = scaled_ptacc * lpbtd;
	ptacc_lbtd = scaled_ptacc * lbtd;
run;

Title "Persistence of Pre-Tax Earnings";
proc reg data=incomew_ranked;
	model scaled_pip1 = scaled_pi;
run;

Title "Persistence of Pre-Tax Earnings Controlled for Size of Book-Tax Differences";
proc reg data=incomew_ranked;
	model scaled_pip1= lbtd scaled_pi pi_lbtd;
run;

Title "Persistence of Pre-Tax Earnings Controlled for Size and Sign of Book-Tax Differences";
proc reg data=incomew_ranked;
	model scaled_pip1= lnbtd lpbtd scaled_pi pi_lnbtd pi_lpbtd;
	TEST pi_lnbtd = pi_lpbtd;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component";
proc reg data=incomew_ranked;
	model scaled_pip1 = scaled_ptcf scaled_ptacc;
	TEST scaled_ptcf = scaled_ptacc;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component Controlled for Size of Book-Tax Differences";
proc reg data=incomew_ranked;
	model scaled_pip1 = lbtd scaled_ptcf ptcf_lbtd scaled_ptacc ptacc_lbtd;
	TEST ptcf_lbtd = ptacc_lbtd;
run;

Title "Persistence of Pre-Tax Earnings by Earnings Component Controlled for Size and Sign of Book-Tax Differences";
proc reg data=incomew_ranked;
	model scaled_pip1 = lnbtd lpbtd scaled_ptcf ptcf_lnbtd ptcf_lpbtd scaled_ptacc ptacc_lnbtd ptacc_lpbtd;
	TEST ptcf_lnbtd = ptcf_lpbtd;
	TEST ptacc_lnbtd = ptacc_lpbtd;
run;

proc means data=incomew_ranked n mean std min max;
    var scaled_pip1 scaled_pi scaled_ptcf scaled_ptacc scaled_dte aveta lnbtd lpbtd small_btd lbtd pi_lnbtd pi_lpbtd pi_lbtd ptcf_lnbtd ptcf_lpbtd ptcf_lbtd ptacc_lnbtd ptacc_lpbtd ptacc_lbtd;
run;